<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<!-- saved from url=(0085)https://courses.cs.washington.edu/courses/cse378/03wi/lectures/mips-asm-examples.html -->
<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8">

<style>@import url( ../style.css );
</style>
<title>MIPS Assembly Language Examples</title>
</head>

<body>
<h2>MIPS Assembly Language Examples</h2>
<h3>Preliminaries</h3>
<ul>
MIPS has 32 "general purpose registers".  As far as the hardware
is concerned, they are all the same, with the sole exception of register 0,
which is hardwired to the value 0.
<p>
As we'll see later, there are software
<b>conventions</b> that restrict the use of registers - an application will
run correctly if it follows these conventions, but may fail if it doesn't and
it interacts with any other software.  (Since all applications interact with
the operating system, unless you find a way to load your application onto
bare hardware, you need to follow the conventions.)&nbsp;
For the moment, we'll only roughly follow some of the conventions.  For each
example below, we'll assume that the PC points to the first instruction in our
code, that we can use the registers called (in assembly language) $t0-$t9
as we please, and that register $gp points to an area in memory that we can
use to hold program variables.
</p><p>
These examples take advantage of the full MIPS instruction set.  The tool we will
be using, <a href="http://www.cs.washington.edu/homes/dugan/cebollita/docs/">Cebollita</a>,
models a processors that implements only a subset.  (This means that if you try to assemble
these programs in Cebollita, some will not work.)
</p></ul>
<h3>Data / Memory Layout</h3>
<ul>
We'll assume all the examples, which are fragments of <code>C</code> programs,
include the following:
<pre>    // none of these allocate any storage
    #define MAX_SIZE  256
    #define IF(a)    if (a) {
    #define ENDIF    }
    typedef struct {
        unsigned char red;      // 'unsigned char' is an unsigned, 8-bit int
        unsigned char green;
        unsigned char blue;
        unsigned char alpha;
    } RGBa;

    // these allocate storage
    int     i;
    int     N = 20;
    char    prompt[] = "Enter an integer:";
    int     A[MAX_SIZE];
    int*    pBArray;
    int     BSize;
    RGBa    background = {0xff, 0xff, 0xff, 0x0};
</pre>
and further that code has already been executed that
initializes pBArray to point to some integer array and 
to set BSize to the size of that array.
(Depending on the C compiler), <b>the memory layout</b>
<a href="https://courses.cs.washington.edu/courses/cse378/03wi/lectures/mem-layout.jpg">looks like this</a>.
</ul>
<h3>A Simple Expression</h3>
<ul>
C code:
<pre>    i = N*N + 3*N
</pre>
"Unoptimized":
<br>
<em>(Note: There are some small disagreements in the syntax of assembler
between SPIM, which is used in the book, and Cebollita,
which is the tool we will be using.
I have tried to follow the conventions of Cebollita here.)</em>
<pre>    lw     $t0, 4($gp)       # fetch N
    mult   $t0, $t0, $t0     # N*N
    lw     $t1, 4($gp)       # fetch N
    ori    $t2, $zero, 3     # 3
    mult   $t1, $t1, $t2     # 3*N
    add    $t2, $t0, $t1     # N*N + 3*N
    sw     $t2, 0($gp)       # i = ...
</pre>
"Optimized":
<pre>    lw     $t0, 4($gp)       # fetch N
    add    $t1, $t0, $zero   # copy N to $t1
    addi   $t1, $t1, 3       # N+3
    mult   $t1, $t1, $t0     # N*(N+3)
    sw     $t1, 0($gp)       # i = ...
</pre>
</ul>
<h3>Array Expression and Inter-Statement Optimizations</h3>
<ul>
C code:
<pre>    A[i] = A[i/2] + 1;
    A[i+1] = -1;
</pre>
"Unoptimized":
<pre># A[i] = A[i/2] + 1;
    lw     $t0, 0($gp)       # fetch i
    srl    $t0, $t0, 1       # i/2
    addi   $t1, $gp, 28      # &amp;A[0]
    sll    $t0, $t0, 2       # turn i/2 into a byte offset (*4)
    add    $t1, $t1, $t0     # &amp;A[i/2]
    lw     $t1, 0($t1)       # fetch A[i/2]
    addi   $t1, $t1, 1       # A[i/2] + 1
    lw     $t0, 0($gp)       # fetch i
    sll    $t0, $t0, 2       # turn i into a byte offset 
    addi   $t2, $gp, 28      # &amp;A[0]
    add    $t2, $t2, $t0     # &amp;A[i]
    sw     $t1, 0($t2)       # A[i] = ...
# A[i+1] = -1;
    lw     $t0, 0($gp)       # fetch i
    addi   $t0, $t0, 1       # i+1
    sll    $t0, $t0, 2       # turn i+1 into a byte offset
    addi   $t1, $gp, 28      # &amp;A[0]
    add    $t1, $t1, $t0     # &amp;A[i+1]
    addi   $t2, $zero, -1    # -1
    sw     $t2, 0($t1)       # A[i+1] = -1
</pre>
"Optimized":
<pre># A[i] = A[i/2] + 1;
    lw     $t0, 0($gp)       # fetch i
    srl    $t1, $t0, 1       # i/2
    sll    $t1, $t1, 2       # turn i/2 into a byte offset (*4)
    add    $t1, $gp, $t1     # &amp;A[i/2] - 28
    lw     $t1, 28($t1)      # fetch A[i/2]
    addi   $t1, $t1, 1       # A[i/2] + 1
    sll    $t2, $t0, 2       # turn i into a byte offset 
    add    $t2, $t2, $gp     # &amp;A[i] - 28
    sw     $t1, 28($t2)      # A[i] = ...
# A[i+1] = -1;
    addi   $t1, $zero, -1    # -1
    sw     $t1, 32($t2)      # A[i+1] = -1
</pre>
</ul>
<h3>#define and <code>if</code> Statement</h3>
<ul>
C code pre-cfront:
<pre>IF (i &lt; N)
    A[i] = 0;
ENDIF
</pre>
C code post-cfront:
<pre>if (i&lt;N) {
    A[i] = 0;
}
</pre>
MIPS assembler:
<pre>    lw     $t0, 0($gp)        # fetch i
    lw     $t1, 4($gp)        # fetch N
    slt    $t1, $t0, $t1      # set $t1 to 1 if $t0 &lt; $t1, to 0 otherwise
    beq    $t1, $zero, skip   # branch if result of slt is 0 (i.e., !(i&lt;N))
    sll    $t0, $t0, 2        # i as a byte offset
    add    $t0, $t0, $gp      # &amp;A[i] - 28
    sw     $zero, 28($t0)     # A[i] = 0
skip:
</pre>
</ul>
<h3>And Mask</h3>
<ul>
C code:
<pre>    background.blue = background.blue * 2;   // Note: overflow...
</pre>
MIPS Assembler:
<pre>    lw    $t0, 1060($gp)      # fetch background
    andi  $t1, $t0, 0xff00    # isolate blue
    sll   $t1, $t1, 2         # times 2
    andi  $t1, $t1, 0xff00    # get rid of overflow
    lui   $t2, 0xffff         # $t2 = 0xffff0000
    ori   $t2, $t2, 0x00ff    # $t2 = 0xffff00ff
    and   $t0, $t0, $t2       # get rid of old value of blue
    or    $t0, $t0, $t1       # new value
    sw    $t0, 1060($gp)      # background = ...
</pre>
</ul>
<h3>Or Mask</h3>
<ul>
C code:
<pre>    // set N to the smallest odd no less than N
    if ( N%2 == 0 ) N++;
</pre>
MIPS Assembler:
<pre>    lw    $t0, 4($gp)         # fetch N
    ori   $t0, $t0, 1         # turn on low order bit
    sw    $t0, 4($gp)         # store result in N
</pre>
</ul>
<h3><code>switch</code> Statement</h3>
<ul>
C code:
<pre>    switch (i) {
        case 0:   A[0] = 0;
	          break;
        case 1:
        case 2:   A[1] = 1;
                  break;
        default:  A[0] = -1;
                  break;
    }
</pre>
For this example, assume the compiler has generated a <em>branch table</em>
and stored it after <code>background</code> in memory (i.e., starting at
offset 1064 from $gp).  The branch table is initialized to hold in successive
locations the absolute addresses of the instructions at labels
<code>is0, is1,</code> and <code>is2</code>.
<p>
MIPS Assembler:
</p><pre>     lw    $t0, 0($gp)          # fetch i
     bltz  $t0, def             # i&lt;0 -&gt; default
     slti  $t1, $t0, 3          # i&lt;3?
     beq   $t1, $zero, def      # no, -&gt; default
     sll   $t0, $t0, 2          # turn i into a byte offset
     add   $t2, $t0, $gp
     lw    $t2, 1064($t2)       # fetch the branch table entry
     jr    $t2                  # go...
is0: sw    $zero, 28($gp)       # A[0] = 0
     j     done
is1: 
is2: addi  $t0, $zero, 1        # = 1
     sw    $t0, 32($gp)         # A[1] = 1
     j     done
def: addi  $t0, $zero, -1       # = -1
     sw    $t0, 28($gp)         # A[0] = -1
     j     done
done:
</pre>
</ul>
<h3><code>for</code> Loop</h3>
<ul>
C code:
<pre>    for (i=0; i&lt;N; i++) {
        A[i] = MAX_SIZE;
    }
</pre>
MIPS Assembler
<pre>    add    $t0, $gp, $zero      # &amp;A[0] - 28
    lw     $t1, 4($gp)          # fetch N
    sll    $t1, $t1, 2          # N as byte offset
    add    $t1, $t1, $gp        # &amp;A[N] - 28
    ori    $t2, $zero, 256      # MAX_SIZE
top:
    sltu   $t3, $t0, $t1        # have we reached the final address?
    beq    $t3, $zero, done     # yes, we're done
    sw     $t2, 28($t0)         # A[i] = 0
    addi   $t0, $t0, 4          # update $t0 to point to next element
    j      top                  # go to top of loop
done:

# NOTE: We have not updated i in memory!
</pre>
</ul>
<p>
</p><hr>
zahorjan@cs.washington.edu 
</body></html>